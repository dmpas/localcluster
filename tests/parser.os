#Использовать asserts

Перем юТест;

// основной метод для тестирования
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт

	юТест = ЮнитТестирование;

	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_ПроверитьПарсерДерева");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьОбработкуТестовогоФайла");

	Возврат ВсеТесты;

КонецФункции

Процедура ТестДолжен_ПроверитьПарсерДерева() Экспорт

	Дерево = СкобкоПарсер.РазобратьДерево("{0,""123
	|456"",20180718120915,{00000000-1234-0000-0000-000000000000}}");

	Утверждения.ПроверитьРавенство(ТипЗнч(Дерево), Тип("Массив"), "Имеем массив");
	Утверждения.ПроверитьРавенство(Дерево.Количество(), 4, "Имеем массив с одним элементом");
	Утверждения.ПроверитьРавенство(ТипЗнч(Дерево[0]), Тип("Число"), "Первый элемент - число");
	Утверждения.ПроверитьРавенство(Дерево[0], 0, "Первый элемент - число `0`");

	Утверждения.ПроверитьРавенство(ТипЗнч(Дерево[1]), Тип("Строка"), "Второй элемент - строка");
	Утверждения.ПроверитьРавенство(Дерево[1], "123
	|456", "Второй элемент - строка с переносом");

	Утверждения.ПроверитьРавенство(ТипЗнч(Дерево[2]), Тип("Дата"), "Третий элемент - дата");
	Утверждения.ПроверитьРавенство(Дерево[2], '20180718120915', "Третий элемент - дата '20180718120915'");

	Утверждения.ПроверитьРавенство(ТипЗнч(Дерево[3]), Тип("Массив"), "Четвёртый элемент - объект");
	Утверждения.ПроверитьРавенство(Дерево[3].Количество(), 1, "Четвёртый элемент - объект с одним элементом");
	Утверждения.ПроверитьРавенство(ТипЗнч(Дерево[3][0]), Тип("УникальныйИдентификатор"), "Четвёртый элемент - объект с одним элементом - ГУИДом");
	Утверждения.ПроверитьРавенство(Дерево[3][0], Новый УникальныйИдентификатор("00000000-1234-0000-0000-000000000000"), 
			"Четвёртый элемент - объект с одним элементом - ГУИДом `00000000-1234-0000-0000-000000000000`");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьОбработкуТестовогоФайла() Экспорт

	Кластер = Новый ЛокальныйКластер("tests/cls.lst");
	Базы = Кластер.ПолучитьИнформационныеБазы();

	Утверждения.ПроверитьРавенство(Базы.Количество(), 2);
	Утверждения.ПроверитьРавенство(Базы[0].Имя, "rts");
	Утверждения.ПроверитьРавенство(Базы[1].Имя, "utnom");

	База = Кластер.НайтиБазу("utnom");
	Утверждения.ПроверитьРавенство(База.УникальныйИдентификатор, Новый УникальныйИдентификатор("c00294bd-645c-46ee-bcc4-7a0649a7c5cd"));

КонецПроцедуры
