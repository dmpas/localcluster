Функция ТекущийСимвол(Знач СкобкоСтрока, Позиция)
	Возврат Сред(СкобкоСтрока, Позиция, 1);
КонецФункции

Процедура ПропуститьСимвол(Позиция)
	Позиция = Позиция + 1;
КонецПроцедуры

Функция КонецСтроки(Знач СкобкоСтрока, Знач Позиция)
	Возврат Позиция > СтрДлина(СкобкоСтрока);
КонецФункции

Процедура ПропуститьПробелы(Знач СкобкоСтрока, Позиция)

	Пока Не КонецСтроки(СкобкоСтрока, Позиция)
		И ПустаяСтрока(ТекущийСимвол(СкобкоСтрока, Позиция)) Цикл

		ПропуститьСимвол(Позиция);

	КонецЦикла;

КонецПроцедуры

Функция ВзятьПодстроку(Знач СкобкоСтрока, Позиция)

	Перем С;
	Результат = Новый Массив;
	ПропуститьСимвол(Позиция);
	С = ТекущийСимвол(СкобкоСтрока, Позиция);
	Пока Не С = """" Цикл

		Результат.Добавить(С);

		ПропуститьСимвол(Позиция);
		С = ТекущийСимвол(СкобкоСтрока, Позиция);
	
	КонецЦикла;

	ПропуститьСимвол(Позиция);

	Возврат СтрСоединить(Результат, "");

КонецФункции

Функция ПреобразоватьЗначение(Знач Представление)
	
	// TODO: Переработать этот хлам
	Попытка
		Возврат Дата(Представление);
	Исключение
	КонецПопытки;

	Попытка
		Возврат Число(Представление);
	Исключение
	КонецПопытки;

	Попытка
		Возврат Новый УникальныйИдентификатор(Представление);
	Исключение
	КонецПопытки;

	// ВызватьИсключение СтрШаблон("Невозможно преобразовать значение `%1`", Представление);
	Возврат Новый НеобработанноеЗначениеПарсераДерева(Представление);

КонецФункции

Функция РазобратьЭлемент(Знач СкобкоСтрока, Позиция)

	Перем С;

	С = ТекущийСимвол(СкобкоСтрока, Позиция);
	Если С = "{" Тогда
		Возврат РазобратьОбъект(СкобкоСтрока, Позиция);
	КонецЕсли;

	Если С = """" Тогда
		
		Результат = Новый Массив;
		Пока С = """" Цикл
			
			Результат.Добавить(ВзятьПодстроку(СкобкоСтрока, Позиция));
			С = ТекущийСимвол(СкобкоСтрока, Позиция);
			Если С = """" Тогда
				Результат.Добавить("""");
			КонецЕсли;

		КонецЦикла;
		
		Возврат СтрСоединить(Результат, "");

	КонецЕсли;

	Результат = Новый Массив;
	Пока Не (С = "," Или С = "}") Цикл
		
		Результат.Добавить(С);

		ПропуститьСимвол(Позиция);
		С = ТекущийСимвол(СкобкоСтрока, Позиция);

	КонецЦикла;

	Возврат ПреобразоватьЗначение(СтрСоединить(Результат, ""));

КонецФункции

Функция МестоОшибки(Знач СкобкоСтрока, Позиция)

	КоличествоСлева = 4;
	КоличествоСправа = 4;

	Лево = Макс(0, Позиция - КоличествоСлева);
	Право = Мин(Позиция + КоличествоСправа, СтрДлина(СкобкоСтрока));

	Возврат Сред(СкобкоСтрока, Лево, Право - Лево + 1);

КонецФункции

Процедура ПропуститьНачалоОбъекта(Знач СкобкоСтрока, Позиция)

	Если Не ТекущийСимвол(СкобкоСтрока, Позиция) = "{" Тогда
		ВызватьИсключение СтрШаблон("Ошибка формата потока! Место: %1", МестоОшибки(СкобкоСтрока, Позиция)); // Троллфэйсъ
	КонецЕсли;

	Позиция = Позиция + 1;
	ПропуститьПробелы(СкобкоСтрока, Позиция);

КонецПроцедуры

Процедура ПропуститьКонецОбъекта(Знач СкобкоСтрока, Позиция)

	Если Не ТекущийСимвол(СкобкоСтрока, Позиция) = "}" Тогда
		ВызватьИсключение СтрШаблон("Пропущен конец объекта! %1", МестоОшибки(СкобкоСтрока, Позиция)); // Троллфэйсъ
	КонецЕсли;

	Позиция = Позиция + 1;
	ПропуститьПробелы(СкобкоСтрока, Позиция);

КонецПроцедуры

Функция РазобратьОбъект(Знач СкобкоСтрока, Позиция)

	ПропуститьНачалоОбъекта(СкобкоСтрока, Позиция);

	Результат = Новый Массив;
	Пока Истина Цикл
		
		Результат.Добавить(РазобратьЭлемент(СкобкоСтрока, Позиция));

		Если ТекущийСимвол(СкобкоСтрока, Позиция) = "," Тогда
			
			ПропуститьСимвол(Позиция);
			ПропуститьПробелы(СкобкоСтрока, Позиция);
			Продолжить;

		КонецЕсли;

		Прервать;

	КонецЦикла;

	ПропуститьКонецОбъекта(СкобкоСтрока, Позиция);

	Возврат Результат;

КонецФункции


Функция РазобратьДерево(Знач СкобкоСтрока) Экспорт

	Возврат РазобратьОбъект(СкобкоСтрока, 1);

КонецФункции
